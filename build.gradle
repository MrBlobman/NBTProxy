buildscript {
    repositories {
        mavenCentral()
        maven {  url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.12.1'
    }
}

group 'io.github.mrblobman'
version '2.1.0'

apply plugin: 'maven-publish'

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.github.hierynomus.license' //Ensure all files have the license header

    license {
        include '**/*.java'
        header = rootProject.file('LICENSE')
        strictCheck true
    }
    assemble.dependsOn licenseFormat

    compileJava {
        sourceCompatibility = JavaVersion.VERSION_1_7
        targetCompatibility = JavaVersion.VERSION_1_7
    }

    repositories {
        mavenLocal()
        //Spigot
        maven { url 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
        mavenCentral()
    }
}

//Make sure that all of the nms sub projects depend on the core
subprojects.each { subproject ->
    if (subproject.name != 'core') {
        subproject.dependencies {
            compile project(':core')
        }
    }
}

jar {
    subprojects.each { subproject ->
        from subproject.sourceSets.main.output
    }
}

task apiJar(type: Jar, dependsOn: [':core:classes', ':core:javadoc']) {
    baseName = "${rootProject.name}-api"

    //Add the sources, compilation output and javadocs for the core project
    //to the api jar
    from project(':core').sourceSets.main.output
    from project(':core').sourceSets.main.allJava
    from project(':core').javadoc.destinationDir

    //Skip the plugin additions because these are for the implementation not the api
    exclude '**/plugin/**'
    exclude 'plugin.yml'
    exclude 'config.yml'
}

artifacts {
    archives apiJar
}

publishing {
    publications {
        api(MavenPublication) {
            artifactId "${rootProject.name}-api"
            artifact apiJar
        }

        impl(MavenPublication) {
            from components.java
        }
    }
}
